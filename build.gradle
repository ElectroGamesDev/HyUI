plugins {
    id 'java'
    alias(libs.plugins.shadow)
    alias(libs.plugins.idea.ext)
    alias(libs.plugins.hytalegradle)
}

dependencies {
    // Html parsing
    shadow libs.jsoup
    implementation libs.jsoup

    // Unit testing
    testImplementation libs.junit
    testRuntimeOnly libs.junit.launcher
}

repositories {
    mavenCentral()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

hytale {
    // Versioning
    patchline = "release"

    // Manifest auto update
    manifest {
        version = project.version.toString()
        // Pre-release 2026.02.11-891910c77
        // Release 2026.02.17-255364b8e, 2026.02.18-f3b8fff95
        serverVersion = "2026.02.18-f3b8fff95"
        author(author_name, "ellie@ellie.au", "https://ellie.au")
        optionalDependencies.put("Buuz135:MultipleHUD", "*");
    }

    // Server run configuration
    includeLocalMods = true
    disableSentry = true
    allowOp = true
}

shadowJar {
    archiveBaseName = project.name
    archiveClassifier = 'debug'

    configurations = [project.configurations.shadow]
}

// Task: Hytale server run

tasks.runServer {
    jvmArgs += "-XX:+AllowEnhancedClassRedefinition"
    environment("HYUI_DEV", "true")
    //args = args + "--log FINEST"
}

// Task: Generate release jar

tasks.sourcesJar {
    dependsOn tasks.updateManifest
}

tasks.register('releaseJar', Jar) {
    group = 'build'
    description = 'Builds the release jar without dev resources'
    archiveClassifier = 'all'

    dependsOn tasks.shadowJar
    dependsOn tasks.test

    def shadowJarTask = tasks.shadowJar as Jar
    from(zipTree(shadowJarTask.archiveFile)) {
        exclude(
                'Common/UI/Custom/Pages/*.html',
                'Common/UI/Custom/*.png',
                'au/ellie/hyui/commands/**',
        )
    }

    destinationDirectory = layout.buildDirectory.dir('libs')
}

// Task: Test configuration

configurations {
    testRuntimeOnly {
        extendsFrom configurations.hytaleServer
    }
}

tasks.test {
    useJUnitPlatform()
    maxHeapSize = "1G"

    // Run only if releaseJar is being executed
    onlyIf {
        gradle.startParameter.taskNames.contains(name)
                || gradle.taskGraph.hasTask(tasks.named("releaseJar").get())
    }

    // Use Hytale's custom log manager during tests
    jvmArgs "-Djava.util.logging.manager=com.hypixel.hytale.logger.backend.HytaleLogManager"

    testLogging {
        events "passed", "failed", "skipped"
    }
}